CONFIG += qt3d

!symbian {
    macx:CONFIG(qt_framework, qt_framework|qt_no_framework) {
        LIBS += -framework Qt3DQuick
        # Makes includes like "#include <Qt3DQuick/foo.h>" work in case of frameworks
        QMAKE_CXXFLAGS += -FQt3DQuick
        # Makes includes like "#include <foo.h>" work in case of frameworks
        INCLUDEPATH += $$[QT_INSTALL_LIBS]/Qt3DQuick.framework/Versions/1/Headers
    } else {
        win32:CONFIG(debug, debug|release) {
            LIBS += -lQt3DQuick$${QT_LIBINFIX}d
        } else {
            LIBS += -lQt3DQuick$${QT_LIBINFIX}
        }
    }
    INCLUDEPATH += $$[QT_INSTALL_HEADERS]/Qt3DQuick

} else {
    load(data_caging_paths)

    INCLUDEPATH+=$$MW_LAYER_PUBLIC_EXPORT_PATH(Qt3DQuick)
}

QT += opengl network declarative
